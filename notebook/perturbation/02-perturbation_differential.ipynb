{
  "cells": [
    {
      "cell_type": "markdown",
      "source": [
        "## Prelims\n\nIn the previous tutorial, *Mixed Symbolic/Numerical Methods for Perturbation Theory - Algebraic Equations*, we discussed how to solve algebraic equations using **Symbolics.jl**. Here, our goal is to extend the method to differential equations. First, we import the following helper functions that were introduced in *Mixed Symbolic/Numerical Methods for Perturbation Theory - Algebraic Equations*."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using Symbolics, SymbolicUtils\n\ndef_taylor(x, ps) = sum([a*x^i for (i,a) in enumerate(ps)])\ndef_taylor(x, ps, p‚ÇÄ) = p‚ÇÄ + def_taylor(x, ps)\n\nfunction collect_powers(eq, x, ns; max_power=100)\n    eq = substitute(expand(eq), Dict(x^j => 0 for j=last(ns)+1:max_power))\n\n    eqs = []\n    for i in ns\n        powers = Dict(x^j => (i==j ? 1 : 0) for j=1:last(ns))\n        push!(eqs, substitute(eq, powers))\n    end\n    eqs\nend\n\nfunction solve_coef(eqs, ps)\n    vals = Dict()\n\n    for i = 1:length(ps)\n        eq = substitute(eqs[i], vals)\n        vals[ps[i]] = Symbolics.solve_for(eq ~ 0, ps[i])\n    end\n    vals\nend"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## The Trajectory of a Ball!\n\nIn the first two examples, we applied the perturbation method to algebraic problems. However, the main power of the perturbation method is to solve differential equations (usually ODEs, but also occasionally PDEs). Surprisingly, the main procedure developed to solve algebraic problems works well for differential equations. In fact, we will use the same two helper functions, `collect_powers` and `solve_coef`. The main difference is in the way we expand the dependent variables. For algebraic problems, the coefficients of $\\epsilon$ are constants; whereas, for differential equations, they are functions of the dependent variable (usually time).\n\nAs the first ODE example, we have chosen a simple and well-behaved problem, which is a variation of a standard first-year physics problem: what is the trajectory of an object (say, a ball or a rocket) thrown vertically at velocity $v$ from the surface of a planet? Assuming a constant acceleration of gravity, $g$, every burgeoning physicist knows the answer: $x(t) = x(0) + vt - \\frac{1}{2}gt^2$. However, what happens if $g$ is not constant? Specifically, $g$ is inversely proportional to the distant from the center of the planet. If $v$ is large and the projectile travels a large fraction of the radius of the planet, the assumption of constant gravity does not hold anymore. However, unless $v$ is large compared to the escape velocity, the correction is usually small. After simplifications and change of variables to dimensionless, the problem becomes\n\n$$\n  \\ddot{x}(t) = -\\frac{1}{(1 + \\epsilon x(t))^2}\n  \\,,\n$$\n\nwith the initial conditions $x(0) = 0$, and $\\dot{x}(0) = 1$. Note that for $\\epsilon = 0$, this equation transforms back to the standard one. Let's start with defining the variables"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "n = 2\n@variables œµ t y[0:n](t) ‚àÇ‚àÇy[0:n]"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Next, we define $x$."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "x = def_taylor(œµ, y[2:end], y[1])"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We need the second derivative of `x`. It may seem that we can do this using `Differential(t)`; however, this operation needs to wait for a few steps because we need to manipulate the differentials as separate variables. Instead, we define dummy variables `‚àÇ‚àÇy` as the placeholder for the second derivatives and define"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "‚àÇ‚àÇx = def_taylor(œµ, ‚àÇ‚àÇy[2:end], ‚àÇ‚àÇy[1])"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "as the second derivative of `x`. After rearrangement, our governing equation is $\\ddot{x}(t)(1 + \\epsilon x(t))^{-2} + 1 = 0$, or"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "eq = ‚àÇ‚àÇx * (1 + œµ*x)^2 + 1"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "The next two steps are the same as the ones for algebraic equations (note that we pass `0:n` to `collect_powers` because the zeroth order term is needed here)"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "eqs = collect_powers(eq, œµ, 0:n)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "and,"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "vals = solve_coef(eqs, ‚àÇ‚àÇy)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Our system of ODEs is forming. Now is the time to convert `‚àÇ‚àÇ`s to the correct **Symbolics.jl** form by substitution:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "D = Differential(t)\nsubs = Dict(‚àÇ‚àÇy[i] => D(D(y[i])) for i in eachindex(y))\neqs = [substitute(first(v), subs) ~ substitute(last(v), subs) for v in vals]"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We are nearly there! From this point on, the rest is standard ODE solving procedures. Potentially we can use a symbolic ODE solver to find a closed form solution to this problem. However, **Symbolics.jl** currently does not support this functionality. Instead, we solve the problem numerically. We form an `ODESystem`, lower the order (convert second derivatives to first), generate an `ODEProblem` (after passing the correct initial conditions), and, finally, solve it."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using ModelingToolkit, DifferentialEquations\n\nsys = ODESystem(eqs, t)\nsys = ode_order_lowering(sys)\nstates(sys)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "# the initial conditions\n# everything is zero except the initial velocity\nu0 = zeros(2n+2)\nu0[3] = 1.0   # y‚ÇÄÀçt\n\nprob = ODEProblem(sys, u0, (0, 3.0))\nsol = solve(prob; dtmax=0.01)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Finally, we calculate the solution to the problem as a function of `œµ` by substituting the solution to the ODE system back into the defining equation for `x`. Note that `ùúÄ` is a number, compared to `œµ`, which is a symbolic variable."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "X = ùúÄ -> sum([ùúÄ^(i-1) * sol[y[i]] for i in eachindex(y)])"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Using `X`, we can plot the trajectory for a range of $ùúÄ$s."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using Plots\n\nplot(sol.t, hcat([X(ùúÄ) for ùúÄ = 0.0:0.1:0.5]...))"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "As expected, as `ùúÄ` becomes larger (meaning the gravity is less with altitude), the object goes higher and stays up for a longer duration. Of course, we could have solved the problem directly using as ODE solver. One of the benefits of the perturbation method is that we need to run the ODE solver only once and then can just calculate the answer for different values of `ùúÄ`; whereas, if we had used the direct method, we would need to run the solver once for each value of `ùúÄ`.\n\n## A Weakly Nonlinear Oscillator\n\nFor the next example, we have chosen a simple example from a very important class of problems, the nonlinear oscillators. As we will see, perturbation theory has difficulty providing a good solution to this problem, but the process is instructive. This example closely follows the chapter 7.6 of *Nonlinear Dynamics and Chaos* by Steven Strogatz.\n\nThe goal is to solve $\\ddot{x} + 2\\epsilon\\dot{x} + x = 0$, where the dot signifies time-derivatives and the initial conditions are $x(0) = 0$ and $\\dot{x}(0) = 1$. If $\\epsilon = 0$, the problem reduces to the simple linear harmonic oscillator with the exact solution $x(t) = \\sin(t)$. We follow the same steps as the previous example."
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "n = 2\n@variables œµ t y[0:n](t) ‚àÇy[0:n] ‚àÇ‚àÇy[0:n]\nx = def_taylor(œµ, y[2:end], y[1])  \n‚àÇx = def_taylor(œµ, ‚àÇy[2:end], ‚àÇy[1])  \n‚àÇ‚àÇx = def_taylor(œµ, ‚àÇ‚àÇy[2:end], ‚àÇ‚àÇy[1])"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "This time we also need the first derivative terms. Continuing,"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "eq = ‚àÇ‚àÇx + 2*œµ*‚àÇx + x\neqs = collect_powers(eq, œµ, 0:n)\nvals = solve_coef(eqs, ‚àÇ‚àÇy)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "Next, we need to replace `‚àÇ`s and `‚àÇ‚àÇ`s with their **Symbolics.jl** counterparts:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "D = Differential(t)\nsubs1 = Dict(‚àÇy[i] => D(y[i]) for i in eachindex(y))\nsubs2 = Dict(‚àÇ‚àÇy[i] => D(D(y[i])) for i in eachindex(y))\nsubs = subs1 ‚à™ subs2\neqs = [substitute(first(v), subs) ~ substitute(last(v), subs) for v in vals]"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We continue with converting 'eqs' to an `ODEProblem`, solving it, and finally plot the results against the exact solution to the original problem, which is $x(t, \\epsilon) = (1 - \\epsilon)^{-1/2} e^{-\\epsilon t} \\sin((1- \\epsilon^2)^{1/2}t)$,"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "sys = ODESystem(eqs, t)\nsys = ode_order_lowering(sys)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "# the initial conditions\nu0 = zeros(2n+2)\nu0[3] = 1.0   # y‚ÇÄÀçt\n\nprob = ODEProblem(sys, u0, (0, 50.0))\nsol = solve(prob; dtmax=0.01)\n\nX = ùúÄ -> sum([ùúÄ^(i-1) * sol[y[i]] for i in eachindex(y)])  \nT = sol.t\nY = ùúÄ -> exp.(-ùúÄ*T) .* sin.(sqrt(1 - ùúÄ^2)*T) / sqrt(1 - ùúÄ^2)    # exact solution\n\nplot(sol.t, [Y(0.1), X(0.1)])"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "The figure is similar to Figure 7.6.2 in *Nonlinear Dynamics and Chaos*. The two curves fit well for the first couple of cycles, but then the perturbation method curve diverges from the true solution. The main reason is that the problem has two or more time-scales that introduce secular terms in the solution. One solution is to explicitly account for the two time scales and use an analytic method called *two-timing*."
      ],
      "metadata": {}
    }
  ],
  "nbformat_minor": 2,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.6.0"
    },
    "kernelspec": {
      "name": "julia-1.6",
      "display_name": "Julia 1.6.0",
      "language": "julia"
    }
  },
  "nbformat": 4
}
